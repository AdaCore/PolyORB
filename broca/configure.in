AC_INIT(src/portableserver.ads)
AC_CONFIG_AUX_DIR(support)
#
# Initialization
#
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(adabroker,1.0pre5)
LIBVERSIONINFO=0:0:0
AC_SUBST(LIBVERSIONINFO)
#
# AdaSockets location
#
ADASOCKETS_INCLUDE="-I\${top_builddir}/adasockets/src -I\${top_srcdir}/adasockets/src -L\${top_builddir}/adasockets/src"
ADASOCKETS_LIBS=""
AC_MSG_CHECKING([whether to use an installed AdaSockets])
has_adasockets=no
AC_ARG_ENABLE(adasockets,
[  --enable-adasockets     Use an existing installation of AdaSockets (no)],
[if adasockets-config --version > /dev/null 2>&1; then
   ADASOCKETS_INCLUDE="`adasockets-config --cflags`"
   ADASOCKETS_LIBS="`adasockets-config --libs`"
   has_adasockets=yes
else
   has_adasockets="no (not found)"
fi],
[])
AM_CONDITIONAL(HAS_ADASOCKETS, test x$has_adasockets = xyes)
AC_SUBST(ADASOCKETS_INCLUDE)
AC_SUBST(ADASOCKETS_LIBS)
AC_MSG_RESULT($has_adasockets)
AC_MSG_CHECKING([whether to build the Interface Repository])
ir_enabled=no
AC_ARG_ENABLE(ir,
[  --enable-ir             Build the Interface Repository (no)],
[ir_enabled=${enableval}])
AM_CONDITIONAL(ENABLE_IR, test x$ir_enabled = xyes)
AC_MSG_RESULT($enable_ir)
#
# Configure AdaSockets if needed (well, in fact it will be checked anyway)
#
if test x$has_adasockets = xyes; then
:
else
AC_CONFIG_SUBDIRS(adasockets)
fi
#
# Check for maintainer mode
#
AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging options],
[GNATFLAGS="-g -O2 -Wall -gnaty3bcefhikmt -gnatfaog -gnatwu -gnatwe -gnatwl"
 debug=true],
[GNATFLAGS="-g -O2"
 debug=false])
AC_SUBST(GNATFLAGS)
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)
#
# Various programs
#
AC_PROG_CC
AC_PROG_INSTALL
AC_CHECK_PROG(RM, rm, rm)
AM_PROG_WORKING_ADA
AM_CROSS_PROG_WORKING_ADA
AC_EXEEXT
AC_CHECK_PROG(GNATMAKE, gnatmake, gnatmake)
AC_CHECK_PROG(GNATCHOP, gnatchop, gnatchop)
AC_CHECK_PROG(PYTHON, python, python)
AM_PROG_LIBTOOL
#
# Check that the GNAT version is recent enough for this version of GLADE
#
AM_ADA_PREREQ(19990629, 3.12p)
#
# Output
#
AC_OUTPUT([Makefile idlac/Makefile idlac/platform.ads src/Makefile
           support/adacompiler idlac/testsuite/Makefile src/adabroker-config
           idls/Makefile idls/cos/Makefile idls/cos/naming/Makefile
           idls/cos/event/Makefile idls/cos/time/Makefile src/ir/Makefile
           examples/Makefile examples/all_functions/Makefile
           examples/all_types/Makefile examples/random/Makefile
	   examples/calculator/Makefile examples/valuetypes/Makefile
           examples/echo/Makefile
	   examples/valuetypes/cycle/Makefile
           examples/valuetypes/forward/Makefile
           man/Makefile
	   cos/Makefile cos/event/Makefile cos/naming/Makefile
           cos/time/Makefile])
