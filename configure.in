#
# $Id$
#
AC_INIT(Dist/xe.ads)
AM_CONFIG_HEADER(Garlic/config.h)
AC_CONFIG_AUX_DIR(support)
#
# Package description
#
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE
#
# Various programs
#
AC_CHECK_PROGS(AWK, [gnuawk gawk awk])
AC_PROG_CC
AM_PROG_WORKING_ADA
AM_CROSS_PROG_WORKING_ADA
AC_EXEEXT
AC_PROG_RANLIB
AC_CHECK_PROGS(SED, [gnused gsed sed])
AC_CHECK_PROGS(TAR, [gtar tar])
AC_CHECK_PROG(AR, ar, ar)
AC_CHECK_PROG(RM, rm, rm)
AC_CHECK_PROG(UNZIP, gunzip, gunzip)
AC_CHECK_PROG(GNATMAKE, gnatmake, gnatmake)
#
# Check for maintainer mode
#
AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging options],
[GNATFLAGS="-g -O2 -Wall -gnatawe"
 LINKTEST="linktest"],
[GNATFLAGS="-O2 -gnatpn"
 LINKTEST=""])
AC_SUBST(GNATFLAGS)
AC_SUBST(LINKTEST)
#
# Check for optimization flags
#
AC_ARG_WITH(optimization,
[  --with-optimization=X   Use X as default optimization for gnatdist (O0)],
[GNATDISTOPT="$withval"],
[GNATDISTOPT="O0"])
AC_SUBST(GNATDISTOPT)
#
# Launching facilities
#
AC_ARG_WITH(launching,
[  --without-launching     Don't include launching facilities],
[
  AC_MSG_CHECKING([launching facilities])
  case ${withval} in
  yes)
     LAUNCHINGSOURCES="\$(LAUNCHINGSOURCES)";
     LAUNCHINGOBJECTS="\$(LAUNCHINGOBJECTS)";
     LAUNCHING_INCLUDED="";
     AC_MSG_RESULT([yes]);;
  *)
     LAUNCHINGSOURCES="";
     LAUNCHINGOBJECTS="";
     LAUNCHING_INCLUDED="--  ";
     AC_MSG_RESULT([no]);;
  esac;
],
[
AC_MSG_CHECKING([launching facilities])
LAUNCHINGSOURCES="\$(LAUNCHINGSOURCES)";
LAUNCHINGOBJECTS="\$(LAUNCHINGOBJECTS)";
LAUNCHING_INCLUDED="";
AC_MSG_RESULT([yes])
])
AC_SUBST(LAUNCHINGSOURCES)
AC_SUBST(LAUNCHINGOBJECTS)
AC_SUBST(LAUNCHING_INCLUDED)
#
# Find GNAT sources
#
AC_GNAT_SOURCE
#
# Check that the GNAT version is recent enough for this version of GLADE
#
AM_ADA_PREREQ(980803, 3.11b-beta)
#
# Platform specific
#
AC_MSG_CHECKING(which platform-specific variant to use)
AC_ARG_WITH(platform,
 [  --with-platform=letter  Select another platform's specific files (debug)],
 [PLATFORM_SPECIFIC="${withval}"],
[case "${target}" in
  sparc-sun-solaris2*) PLATFORM_SPECIFIC=s;;
  sparc-sun-sunos4*)   PLATFORM_SPECIFIC=u;;
  i?86-*-solaris2*)    PLATFORM_SPECIFIC=s;;  
  *-linux*)            PLATFORM_SPECIFIC=l;;
  *-freebsd*)          PLATFORM_SPECIFIC=f;;
  *-opennt*)           PLATFORM_SPECIFIC=p;;
  alpha*-*-osf*)       PLATFORM_SPECIFIC=a;;
  i?86-*-cygwin32*)    PLATFORM_SPECIFIC=w;;
  i?86-*-mingw32*)     PLATFORM_SPECIFIC=w;;
  mips*-sgi-irix*)     PLATFORM_SPECIFIC=g;;
  mips*-sni-sysv4*)    PLATFORM_SPECIFIC=x;;
  hppa*-hp-hpux*)      PLATFORM_SPECIFIC=h;;
  powerpc-*-aix4*)     PLATFORM_SPECIFIC=b;;
  *-*-rtems*)          PLATFORM_SPECIFIC=r;;
  *)                   ]AC_MSG_ERROR([Unsupported platform "${target}"])[;;
esac])
AC_SUBST(PLATFORM_SPECIFIC)
[case "${PLATFORM_SPECIFIC}" in
   s) PSNAME="Solaris";;
   u) PSNAME="SunOS";;
   a) PSNAME="OSF";;
   l) PSNAME="Linux";;
   f) PSNAME="FreeBSD";;
   p) PSNAME="Open NT";;
   w) PSNAME="Windows NT";;
   g) PSNAME="SGI";;
   x) PSNAME="SNI";;
   h) PSNAME="HP";;
   b) PSNAME="AIX";;
   e) PSNAME="Empty";;
   r) PSNAME="RTEMS";;
   *) PSNAME="Unknown (${PLATFORM_SPECIFIC})";;
esac]
AC_SUBST(PSNAME)
AC_MSG_RESULT($PSNAME)
AC_MSG_CHECKING(preserve the original file properties with)
[case "${PLATFORM_SPECIFIC}" in
   w) CP_PRESERVE="-lf";;
   *) CP_PRESERVE="-p";;
esac]
AC_SUBST(CP_PRESERVE)
AC_MSG_RESULT($CP_PRESERVE)
#
# Local launcher. Linux does not support local launching of programs with
# LinuxThreads with libc1. This crashes the machine.
#
AC_MSG_CHECKING(if platform supports local launching)
case "${target}" in
  *-linux*libc1*)	SUPPORTS_LOCAL_LAUNCH=False; AC_MSG_RESULT(no);;
  *)            	SUPPORTS_LOCAL_LAUNCH=True; AC_MSG_RESULT(yes);;
esac
AC_SUBST(SUPPORTS_LOCAL_LAUNCH)
#
# Remote shell command
#
AC_MSG_CHECKING(which remote shell command to use)
AC_ARG_WITH(rshcmd,
 [  --with-rshcmd=command   Set alternate remote shell command],
 [RSH_CMD="${withval}"],
 [case "${target}" in
   hppa*-hp-hpux*)      RSH_CMD="remsh";;
   *)                   RSH_CMD="rsh -n";;
  esac])
AC_SUBST(RSH_CMD)
AC_MSG_RESULT($RSH_CMD)
#
# Remote shell flags
#
AC_MSG_CHECKING(which remote shell options to use)
AC_ARG_WITH(rshopt,
 [  --with-rshopt=command   Set remote shell options],
 [RSH_OPT="${withval}"],
 [case "${target}" in
   hppa*-hp-hpux*)      RSH_OPT="-n";;
   *)                   RSH_OPT="";;
  esac])
AC_SUBST(RSH_OPT)
AC_MSG_RESULT($RSH_OPT)
#
# Protocol list
#
AC_ARG_WITH(protocols,
[  --with-protocols=list   Enumerate protocol list],
[
  AC_MSG_CHECKING([default protocols])
  LIST="";
  PROTOCOLSSOURCES="";
  PROTOCOLSOBJECTS="";
  DEFPROTOCOLNAME="";
  DEFPROTOCOLDATA="";
  TCP_INCLUDED="--  ";
  for P in ${withval}; do
     if test -z "${DEFPROTOCOLNAME}"; then
       DEFPROTOCOLNAME=${P};
     fi
     case ${P} in
     TCP|tcp) 
        LIST="${LIST} tcp";
        PROTOCOLSSOURCES="${PROTOCOLSSOURCES} \$(TCPSOURCES)";
        PROTOCOLSOBJECTS="${PROTOCOLSOBJECTS} \$(TCPOBJECTS)";
        TCP_INCLUDED="";;
     none)
        LIST="none";
        ;;
      *) 
        AC_MSG_ERROR([unknown protocol ${P}])
        ;;
      esac;
  done
  AC_MSG_RESULT([${LIST}])
],
[
AC_MSG_CHECKING([default protocols])
PROTOCOLSSOURCES="\$(TCPSOURCES)";
PROTOCOLSOBJECTS="\$(TCPOBJECTS)";
TCP_INCLUDED=""
DEFPROTOCOLNAME="tcp";
DEFPROTOCOLDATA="";
AC_MSG_RESULT([tcp])
])
if test -z "$TCP_INCLUDED"; then
  if test -f $srcdir/Garlic/5${PLATFORM_SPECIFIC}gtplsp.ads; then
    S_GTPLSP_ADS="5${PLATFORM_SPECIFIC}gtplsp.ads";
  else
    S_GTPLSP_ADS="5egtplsp.ads"
  fi
  if test -f $srcdir/Garlic/5${PLATFORM_SPECIFIC}gtplsp.adb; then
    S_GTPLSP_ADB="5${PLATFORM_SPECIFIC}gtplsp.adb"
    # It is important that the body be first
    TCP_PLATFORM_SOURCES="s-gtplsp.adb s-gtplsp.ads"
  else
    S_GTPLSP_ADB=""
    TCP_PLATFORM_SOURCES="s-gtplsp.ads"
  fi;
  AC_SUBST(S_GTPLSP_ADS)
  AC_SUBST(S_GTPLSP_ADB)
  AC_SUBST(TCP_PLATFORM_SOURCES)
  AC_ARG_WITH(thread-blocking-io,
  [  --with-thread-blocking-io
                          Pretend that system has thread blocking I/Os],
  [
   if test "$withval" = "yes"; then
     ISTHRBIO="yes"
   elif test "$withval" = "no"; then
     ISTHRBIO="no"
   else
     AC_MSG_ERROR([unknown value ${withval}])
   fi
  ],
  [
    if test "${PLATFORM_SPECIFIC}" = "w"; then
      ISTHRBIO="yes"
    else
      ISTHRBIO="test"
    fi
  ])
 if test "$ISTHRBIO" = "yes"; then
   :
 else
   PROTOCOLSSOURCES="${PROTOCOLSSOURCES} \$(NOBLTCPSOURCES)"
   PROTOCOLSOBJECTS="${PROTOCOLSOBJECTS} \$(NOBLTCPOBJECTS)"
 fi
fi
AC_SUBST(PROTOCOLSSOURCES)
AC_SUBST(PROTOCOLSOBJECTS)
AC_SUBST(TCP_INCLUDED)
AC_SUBST(ISTHRBIO)
#
# Default storage location
#
AC_ARG_WITH(data-location,
[  --with-data-location=x  Default data location (x=<name>://<data>)],
[
DEFSTORAGENAME=`echo ${withval} | ${SED} 's,^\(.*\)://.*$,\1,'`
DEFSTORAGEDATA=`echo ${withval} | ${SED} 's,^.*://\(.*\)$,\1,'`
],
[
DEFSTORAGENAME="dfs"
DEFSTORAGEDATA=""
])
AC_MSG_CHECKING([for default data location])
AC_MSG_RESULT([${DEFSTORAGENAME}://${DEFSTORAGEDATA}])
AC_SUBST(DEFSTORAGENAME)
AC_SUBST(DEFSTORAGEDATA)
#
# Default boot location
#
AC_ARG_WITH(boot-location,
[  --with-boot-location=x  Default boot location (x=<name>://<data>)],
[
DEFPROTOCOLNAME=`echo ${withval} | ${SED} 's,^\(.*\)://.*$,\1,'`
DEFPROTOCOLDATA=`echo ${withval} | ${SED} 's,^.*://\(.*\)$,\1,'`
],
[
if test -z "${TCP_INCLUDED}"; then
  DEFPROTOCOLDATA=`echo 000$$ | ${SED} 's,^.*\(...\)$,localhost:5\1,'`
else
  BOOT_SERVER=""
fi
])
AC_MSG_CHECKING([for default boot location])
AC_MSG_RESULT([${DEFPROTOCOLNAME}://${DEFPROTOCOLDATA}])
AC_SUBST(DEFPROTOCOLNAME)
AC_SUBST(DEFPROTOCOLDATA)
#
# Filter list
#
AC_ARG_WITH(filters,
[  --with-filters=list     Enumerate filter list (default=zip)],
[
  FILTERSLIST="${withval}";
],
[
  FILTERSLIST="zip"
])
AC_MSG_CHECKING([default filters])
[
if test -d Garlic; then
  :
else
  mkdir Garlic
fi
FILTERSMAKEFILE=Garlic/Makefile.fil;
FILTERSOURCES=""
FILTEROBJECTS=""
FILTERINSTALL=""
FILTEREXTRALIBS=""
rm -f ${FILTERSMAKEFILE}
for F in ${FILTERSLIST}; do
  if test -f ${srcdir}/Garlic/Makefile.${F}; then
    cat ${srcdir}/Garlic/Makefile.${F} >>${FILTERSMAKEFILE}
    echo "" >>${FILTERSMAKEFILE}
    FILTERSSOURCES="${FILTERSSOURCES} \$(${F}SOURCES)"
    FILTERSOBJECTS="${FILTERSOBJECTS} \$(${F}OBJECTS)"
    FILTERSINSTALL="${FILTERSINSTALL} \$(${F}INSTALL)"
    FILTERSEXTRALIBS="${FILTERSEXTRALIBS} \$(${F}EXTRAS)"
  else
    ]AC_MSG_ERROR([unknown filter ${F}])[
  fi;
done
]
AC_MSG_RESULT([${FILTERSLIST}])
#
# Force zip lib installation when zip filter included
#
AC_ARG_ENABLE(glade-zlib,
[  --enable-glade-zlib     Force to install glade zip library],
[INSTALLZIPLIB="yes"],
[INSTALLZIPLIB="no"])
#
# Libraries used for filters
#
case ${FILTERSLIST} in
  *zip*)
    if test $INSTALLZIPLIB = no; then
      AC_CHECK_LIB(z,zlibVersion)
      if test $ac_cv_lib_z_zlibVersion = no; then
        INSTALLZIPLIB=yes
      fi
    fi
    AC_MSG_CHECKING([whether zip lib installation is needed])
    if test ${INSTALLZIPLIB} = yes; then
      FILTERSINSTALL="${FILTERSINSTALL} install-zlib";
    fi
    AC_MSG_RESULT([${INSTALLZIPLIB}])
    ;;
esac
AC_SUBST_FILE(FILTERSMAKEFILE)
AC_SUBST(FILTERSSOURCES)
AC_SUBST(FILTERSOBJECTS)
AC_SUBST(FILTERSEXTRALIBS)
AC_SUBST(FILTERSINSTALL)
#
# Extra libraries
#
AC_ARG_WITH(extra-libs,
 [  --with-extra-libs=libs  Add extra libraries when building a program],
 [EXTRA_LIBS="${withval}"])
AC_SUBST(EXTRA_LIBS)
#
# Header files, only when not cross-compiling
#
if test $target = $host; then
AC_CHECK_HEADERS([stdio.h sys/types.h sys/socket.h errno.h netdb.h string.h])
AC_CHECK_HEADERS([netinet/in.h signal.h fcntl.h termio.h termios.h sys/file.h])
AC_CHECK_HEADERS([sys/ioctl.h netinet/tcp.h sys/systeminfo.h poll.h stropts.h])
AC_CHECK_HEADERS([sys/conf.h])
fi
#
# Output
#
AC_OUTPUT([Makefile
           Dist/Makefile
           Garlic/Makefile
           Garlic/s-gaplsp.ads
           Garlic/s-garser.adb
           Examples/check-config
           Examples/Makefile
           Examples/Bank/Makefile
           Examples/Eratho/Makefile
           Examples/Eratho/absolute/Makefile
           Examples/Eratho/cycle/Makefile
           Examples/Eratho/dynamic/Makefile
           Examples/Eratho/spiral/Makefile
           Examples/Filters/Makefile
           Examples/Messages/Makefile
           Examples/MultiPro/Makefile
           Examples/MultiSto/Makefile
           Examples/Reconnection/Makefile
           Examples/LightBank/Makefile])
