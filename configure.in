AC_INIT(src/polyorb.ads)
AC_CONFIG_AUX_DIR(support)

##########################################
# Initialization.
##########################################

AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(polyorb,0.1)
LIBVERSIONINFO=0:0:0
AC_SUBST(LIBVERSIONINFO)

POLYORB_MAJOR_VERSION=0
POLYORB_MINOR_VERSION=1
AC_SUBST(POLYORB_MAJOR_VERSION)
AC_SUBST(POLYORB_MINOR_VERSION)

##########################################
# Various programs.
##########################################

AC_PROG_CC
AC_PROG_INSTALL
AC_CHECK_PROG(RM, rm, rm)
AC_CHECK_PROGS(AWK, [gnuawk gawk awk])
AM_PROG_WORKING_ADA
AM_CROSS_PROG_WORKING_ADA
AC_EXEEXT
AM_PROG_GNATMAKE
AM_CROSS_PROG_GNATMAKE
AM_CROSS_PROG_CC
AC_CHECK_PROG(GNATCHOP, gnatchop, gnatchop)
AC_CHECK_PROG(PYTHON, python, python)
AM_ENABLE_SHARED(no)
AM_ENABLE_STATIC(yes)

##########################################
# Check that the GNAT version is recent 
# enough for this version of PolyORB.
##########################################

AM_ADA_PREREQ(20010503, 3.14a1)

##########################################
# Protocol personalities list.
##########################################

AC_MSG_CHECKING([default protocol personalities])
AC_ARG_WITH(proto-perso,
[  --with-proto-perso=x    Enumerate protocol personalities],
[
  PROTO_LIST=""
  for P in ${withval}
  do
     if test -f ${srcdir}/src/${P}/polyorb-binding_data-*.ads
     then
        PROTO_LIST="${PROTO_LIST} $P "
     else
        AC_MSG_ERROR([unknown protocol personality ${P}])
     fi
  done
],
[
  PROTO_LIST=" giop "
])
AC_MSG_RESULT(${PROTO_LIST})

##########################################
# Application personalities list
##########################################

AC_MSG_CHECKING([default application personalities])
AC_ARG_WITH(appli-perso,
[  --with-appli-perso=x    Enumerate application personalities],
[
  APPLI_LIST=""
  for P in ${withval}
  do
     if test -d ${srcdir}/src/${P} -o ${P} = "dsa"
     then
        APPLI_LIST="${APPLI_LIST} $P "
     else
        AC_MSG_ERROR([unknown application personality ${P}])
     fi
  done
],
[
   APPLI_LIST=" corba "
])
AC_MSG_RESULT(${APPLI_LIST})

##########################################
# Services list.
##########################################

AC_MSG_CHECKING([default services])
AC_ARG_WITH(services,
[  --with-services=x       Enumerate services],
[
  SERVICE_LIST=""
  for S in ${withval}
  do
     if test -f ${srcdir}/cos/${S}/Makefile.am
     then
        SERVICE_LIST="${SERVICE_LIST} $S "
     else
        AC_MSG_ERROR([unknown service ${S}])
     fi
  done
],
[
  SERVICE_LIST=" "
])
AC_MSG_RESULT(${SERVICE_LIST})
AC_SUBST(SERVICE_LIST)

##########################################
# Update Makefiles' subdirectory lists
##########################################

APPLI_DIRS=""
APPLI_EXES=""
APPLI_INCS=""
APPLI_LIBS=""

for P in ${APPLI_LIST}
do
   APPLI_DIRS="${APPLI_DIRS} "'$'"(${P}_dir)"
   APPLI_LIBS="${APPLI_LIBS} "'$'"(${P}_lib)"
   APPLI_INCS="${APPLI_INCS} "'$'"(${P}_inc)"
   APPLI_EXES="${APPLI_EXES} "'$'"(${P}_exe)"
done

AC_SUBST(APPLI_DIRS)
AC_SUBST(APPLI_EXES)
AC_SUBST(APPLI_INCS)
AC_SUBST(APPLI_LIBS)

PROTO_DIRS=""
PROTO_INCS=""
PROTO_LIBS=""

for P in ${PROTO_LIST}
do
   PROTO_DIRS="${PROTO_DIRS} "'$'"(${P}_dir)"
   PROTO_INCS="${PROTO_INCS} "'$'"(${P}_inc)"
   PROTO_LIBS="${PROTO_LIBS} "'$'"(${P}_lib)"
done

AC_SUBST(PROTO_DIRS)
AC_SUBST(PROTO_INCS)
AC_SUBST(PROTO_LIBS)

SERVICE_DIRS=""
SERVICE_INCS=""
SERVICE_EXES=""

for P in ${SERVICE_LIST}
do
   SERVICE_DIRS="${SERVICE_DIRS} "'$'"(${P}_dir)"
   SERVICE_INCS="${SERVICE_INCS} "'$'"(${P}_inc)"
   SERVICE_EXES="${SERVICE_EXES} "'$'"(${P}_exe)"
done

AC_SUBST(SERVICE_DIRS)
AC_SUBST(SERVICE_INCS)
AC_SUBST(SERVICE_EXES)

##########################################
# Test : if SOAP personality is built,
#        then XmlAda should be configured
##########################################

XMLADA_INCS=""
XMLADA_LIBS=""
has_xmlada=no

AC_MSG_CHECKING([XmlAda])
if xmlada-config --version 2>&1 | grep "^XmlAda" > /dev/null 2>&1; then
   XMLADA_INCS="`xmlada-config --cflags`"
   XMLADA_LIBS="`xmlada-config --libs`"
   has_xmlada=yes
else
   has_xmlada=no
fi
AC_MSG_RESULT($has_xmlada)

AM_CONDITIONAL(HAS_XMLADA, test x"$has_xmlada" = xyes)
AC_SUBST(XMLADA_INCS)
AC_SUBST(XMLADA_LIBS)

soap_enabled=`echo ${PROTO_LIST} | ${AWK} '/soap/{print "yes"}'`

if test x"$soap_enabled" = xyes -a x"$has_xmlada" != xyes
then
   AC_MSG_ERROR("Protocol personality SOAP requires XmlAda")
fi

##########################################
# Test : if IR is built, 
#        then CORBA must be built
##########################################

corba_enabled=`echo ${APPLI_LIST} | ${AWK} '/corba/{print "yes"}'`
ir_enabled=`echo ${SERVICE_LIST} | ${AWK} '/ir/{print "yes"}'`

if test x$ir_enabled = xyes -a x$corba_enabled != xyes
then
   AC_MSG_ERROR("Interface Repository requires CORBA application personality")
fi

##########################################
# Check for maintainer (debug) mode.
##########################################

AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging options],
[GNATFLAGS="-g -O2 -gnatfa -fstack-check -gnatoy -gnatwae -Wall"
 BARGS=-E
 debug=true],
[GNATFLAGS="-g -O2"
 BARGS=
 debug=false])
AC_SUBST(GNATFLAGS)
AC_SUBST(BARGS)
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

##########################################
# We change $host in order to force the generation of a libtool for the
# target, and not for the host.
# XXX if someone knows a clean way to do that, be my guest...
##########################################

host_tmp=$host
host=$target
AM_PROG_LIBTOOL
host=$host_tmp

##########################################
# Build polyorb-setup-* files
##########################################

PROTO_CLIENT_WITHS=${srcdir}/proto_client_withs.adb
echo "" > ${PROTO_CLIENT_WITHS}
PROTO_SERVER_WITHS=${srcdir}/proto_server_withs.adb
echo "" > ${PROTO_SERVER_WITHS}

for P in ${PROTO_LIST}
do
   U=`${AWK} '/^package/{print $2}' ${srcdir}/src/${P}/polyorb-binding_data-*.ads`
   cat >>${PROTO_CLIENT_WITHS} <<EOF
-- ${P} --
with ${U};
pragma Elaborate_All (${U});
pragma Warnings (Off, ${U});

EOF

   U=`${AWK} '/^package/{print $2}' ${srcdir}/src/${P}/polyorb-setup-tcp_access_points-*.ads`
   cat >>${PROTO_SERVER_WITHS} <<EOF
-- ${P} --
with ${U};
pragma Elaborate_All (${U});
pragma Warnings (Off, ${U});

EOF
done

AC_SUBST_FILE(PROTO_CLIENT_WITHS)
AC_SUBST_FILE(PROTO_SERVER_WITHS)

##########################################
# Output generated files
##########################################

AC_OUTPUT([Makefile
	   compilers/Makefile compilers/gnatprfh/Makefile
	   compilers/ciao/Makefile compilers/idlac/Makefile
	   compilers/idlac/testsuite/Makefile
	   compilers/idlac/platform.ads
	   src/Makefile src/corba/Makefile src/giop/Makefile 
           src/moma/Makefile src/soap/Makefile src/srp/Makefile
           src/polyorb-setup-client.adb src/polyorb-setup-server.adb
	   idls/Makefile idls/cos/Makefile idls/cos/event/Makefile
	   idls/cos/naming/Makefile idls/cos/time/Makefile
	   cos/Makefile cos/event/Makefile cos/naming/Makefile
	   cos/time/Makefile cos/ir/Makefile
	   examples/Makefile 
	   examples/corba/Makefile examples/corba/all_types/Makefile 
	   examples/corba/all_functions/Makefile examples/corba/echo/Makefile
	   examples/dsa/Makefile examples/moma/Makefile examples/polyorb/Makefile
	   support/adacompiler support/native-adacompiler
           support/linker polyorb-config])

##########################################
# Clean up
##########################################

${RM} ${PROTO_CLIENT_WITHS}
${RM} ${PROTO_SERVER_WITHS}

##########################################
# Copy files
##########################################

#
# Some files need to be copied verbatim from the source tree to the
# build directory (only necessary when source dir != build dir).
#
# XXX Should we add src/soap/gnat.adc ?

for a in src/gnat.adc src/corba/gnat.adc src/moma/gnat.adc
do
  if test ! -f "$a"
  then
    echo "creating $a"
    cp $srcdir/$a $a
  fi
done
for a in linker adacompiler
do
  chmod a+x support/$a
done

