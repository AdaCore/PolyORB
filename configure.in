#
# $Id$
#
AC_INIT([Dist/xe.ads])
#
# Package description
#
PACKAGE=pcs
AC_SUBST(PACKAGE)
VERSION=1.0
AC_SUBST(VERSION)
#
# Various programs
#
AC_PROG_CC
AC_CHECK_PROGS(SED, [gnused gsed sed])
AC_CHECK_PROGS(AWK, [gnuawk gawk awk])
AC_DEFUN(AM_PROG_INSTALL,
[AC_REQUIRE([AC_PROG_INSTALL])
test -z "$INSTALL_SCRIPT" && INSTALL_SCRIPT='${INSTALL_PROGRAM}'
AC_SUBST(INSTALL_SCRIPT)dnl
])
AM_PROG_INSTALL
AC_CANONICAL_SYSTEM
AC_ARG_PROGRAM
#
# Check for maintainer mode
#
AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging options],
[GNATFLAGS="-g -gnata -gnato"],
[GNATFLAGS="-O2 -gnatn"])
AC_SUBST(GNATFLAGS)
#
# Include or not launching facilities
#
AC_ARG_WITH(launching,
[  --without-launching     Don't include launching facilities],
[
  AC_MSG_CHECKING([launching facilities])
  case ${withval} in
  yes)
     LAUNCHINGSOURCES="\$(LAUNCHINGSOURCES)";
     LAUNCHINGOBJECTS="\$(LAUNCHINGOBJECTS)";
     LAUNCHING_INCLUDED="";
     AC_MSG_RESULT([yes]);;
  *)
     LAUNCHINGSOURCES="";
     LAUNCHINGOBJECTS="";
     LAUNCHING_INCLUDED="--  ";
     AC_MSG_RESULT([no]);;
  esac;
],
[
AC_MSG_CHECKING([launching facilities])
LAUNCHINGSOURCES="\$(LAUNCHINGSOURCES)";
LAUNCHINGOBJECTS="\$(LAUNCHINGOBJECTS)";
LAUNCHING_INCLUDED="";
AC_MSG_RESULT([yes])
])
AC_SUBST(LAUNCHINGSOURCES)
AC_SUBST(LAUNCHINGOBJECTS)
AC_SUBST(LAUNCHING_INCLUDED)
#
# Protocol list
#
AC_ARG_WITH(protocols,
[  --with-protocols        Enumerate protocol list],
[
  AC_MSG_CHECKING([default protocols])
  LIST="";
  PROTOCOLSSOURCES="";
  PROTOCOLSOBJECTS="";
  TCP_INCLUDED="--  ";
  for P in "${withval}"; do
     case ${P} in
     TCP|tcp) 
        LIST="${LIST} tcp";
        PROTOCOLSSOURCES="${PROTOCOLSSOURCES} \$(TCPSOURCES)";
        PROTOCOLSOBJECTS="${PROTOCOLSOBJECTS} \$(TCPOBJECTS)";
        TCP_INCLUDED="";;
     no)
        LIST="none";
        ;;
      *) 
        AC_MSG_ERROR([unknown protocol ${P}])
        ;;
      esac;
  done
  AC_MSG_RESULT([${LIST}])
],
[
AC_MSG_CHECKING([default protocols])
PROTOCOLSSOURCES="\$(TCPSOURCES)";
PROTOCOLSOBJECTS="\$(TCPOBJECTS)";
TCP_INCLUDED=""
AC_MSG_RESULT([tcp])
])
AC_SUBST(PROTOCOLSSOURCES)
AC_SUBST(PROTOCOLSOBJECTS)
AC_SUBST(TCP_INCLUDED)
#
# Find GNAT sources
#
AC_DEFUN([AC_GNAT_SOURCE],
[
  AC_MSG_CHECKING([for GNAT sources])
  if test -f gnat/osint.ads; then
    GNAT_SOURCE=../gnat
    AC_MSG_RESULT(gnat)
  elif test -f ada/osint.ads; then
    GNAT_SOURCE=../ada
    AC_MSG_RESULT(ada)
  elif test -f $srcdir/gnat/osint.ads; then
    GNAT_SOURCE=$srcdir/gnat
    AC_MSG_RESULT($srcdir/gnat)
  elif test -f $srcdir/ada/osint.ads; then
    GNAT_SOURCE=$srcdir/ada
    AC_MSG_RESULT($srcdir/ada)
  else
    AC_MSG_ERROR([no sources found])
  fi
  AC_SUBST(GNAT_SOURCE)
])
AC_GNAT_SOURCE
#
# Make
#
AC_PROG_MAKE_SET
#
# Platform specific
#
AC_MSG_CHECKING(which platform-specific variant to use)
AC_ARG_WITH(platform,
 [  --with-platform=letter  Select another platform's specific files (debug)],
 [PLATFORM_SPECIFIC="${withval}"],
[case "${target}" in
  sparc-sun-solaris2*) PLATFORM_SPECIFIC=s;;
  sparc-sun-sunos4*)   PLATFORM_SPECIFIC=u;;
  i?86-*-linux)        PLATFORM_SPECIFIC=a;;
  mips-sgi-irix*)      PLATFORM_SPECIFIC=g;;
  *)                   ]AC_MSG_ERROR([Unsupported platform "${target}"])[;;
esac])
AC_SUBST(PLATFORM_SPECIFIC)
[case "${PLATFORM_SPECIFIC}" in
   s) PSNAME="Solaris";;
   u) PSNAME="SunOS";;
   a) PSNAME="Linux";;
   g) PSNAME="SGI";;
   e) PSNAME="Empty";;
   *) PSNAME="Unknown (${PLATFORM_SPECIFIC})";;
esac]
AC_MSG_RESULT($PSNAME)
#
# Find get_errno or __get_errno
#
AC_CHECK_LIB(gnat, get_errno, [CERRNO=get_errno],
[AC_CHECK_LIB(gnat, __get_errno, [CERRNO=__get_errno],
AC_ERROR("Cannot find get_errno or __get_errno in GNAT library"))])
AC_SUBST([CERRNO])
#
# Extra libraries
#
AC_ARG_WITH(extra-libs,
 [  --with-extra-libs=libs  Add extra libraries when building a program],
 [EXTRA_LIBS="${withval}"])
AC_SUBST(EXTRA_LIBS)
#
# Output
#
AC_OUTPUT([Makefile
           Dist/Makefile Dist/xe-gcc Dist/xe-gcc-filter
           Garlic/Makefile Garlic/s-gaprco.adb Garlic/s-garser.adb
           Garlic/s-gaosli.ads
           Examples/check-config
           Examples/Makefile
           Examples/Eratho/Makefile
           Examples/Eratho/absolute/Makefile
           Examples/Eratho/cycle/Makefile
           Examples/Eratho/dynamic/Makefile
           Examples/Eratho/spiral/Makefile
           Examples/RACW/Makefile])
